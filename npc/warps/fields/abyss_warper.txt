//===== rAthena Script =======================================
//= Abyss Cave Warper
//===== By: ==================================================
//= erKURITA
//===== Current Version: =====================================
//= 1.4
//===== Compatible With: =====================================
//= Any Athena Version
//===== Description: =========================================
//= Warper to Abyss Cave. and warper out
//===== Additional Comments: =================================
//= 1.0 Added by Nexon [Nexon]
//= 1.1 Removed Duplicates [Silent]
//= 1.2 Fixed tab - missing pillar's appeared [Lupus]
//= 1.3 Updated Abyss Lake entrance based on official [SinSloth]
//= 1.3a Removed .GATs [Lupus]
//= 1.4 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//=============================================================
//
// this script translated by :
// newsubzero @idAthena
//
//==============================================================

hu_fild05,168,304,0	script	Column#abyss1	111,{

	mes "^3355FFAda 3 keanehan";
	mes "terbentuk pada lekukan";
	mes "permukaan relif ini";
	mes "Tampaknya merupakan bagian";
	mes "tertentu tubuh naga";
	mes "akan cocok dengan sempurna";
	mes "pada relif lekukan ini.^000000";

	if((countitem(1035)) && (countitem(1036)) && (countitem(1037)))
	{
		next;
		switch( select( "Masukan bagian tubuh naga ","Makan bagian tubuh naga" ) )
		{
			case 1:
				mes "^3355FFKamu sangat hati2 memasang";
				mes "Dragon Canine pada lekukan itu";
				mes "dan tiba2 *gururururubag* ";
				mes "kamu mendengar suara bergemuruh";
				mes "di bagian relif itu^000000";
				specialeffect EF_WINDHIT;
				next;
				mes "^3355FFKamu perlahan2 memasukan";
				mes "Dragon Scale pada bagian lain";
				mes "relif lekukan *gurubag*, sehingga memicu";
				mes "gempa kecil, dari relif itu.^000000";
				specialeffect EF_WINDHIT;
				next;
				mes "^3355FFkamu berhati2 mamasang";
				mes "Dragon Tail pada relif terakhir";
				mes "tiba2 *cring* muncul sinar";
				mes "dari celah retakan itu...^000000";
				specialeffect EF_WINDHIT;
				next;
				mes "^3355FFTanah di bawah kaki kamu";
				mes "tiba tiba berguncang";
				mes "*gerreeregrererger*^000000";
				mes "*waaaaaaa* kamu masuk lubang";
				specialeffect EF_BOWLINGBASH;
				donpcevent "AbyssWarp::OnWarp";
				specialeffect2 EF_PORTAL;
				delitem 1035,1;
				delitem 1036,1;
				delitem 1037,1;
				close2;
				warp "hu_fild05",184,204;
				end;

			case 2:
				mes "^3355FF*glek* Kamu memakan";
				mes "Dragon's Canine, Dragon";
				mes "Scale, dan Dragon Tail ";
				mes "ke dalam mulut secara perlahan";
				mes "mengunyahnya. tetapi tidak terjadi";
				mes "apapun dan rasanya pun aneh";
				mes "*hueeekx's* kamu memuntahkannya.^000000";
				next;
				emotion e_rice,1;
				delitem 1035,1;
				delitem 1036,1;
				delitem 1037,1;
				percentheal -10,0;
				close;
		}
	}
	close;
}

hu_fild05,171,211,0	script	Column#abyss2	111,1,1,{

	mes "^3355FFRelif ini terlihat";
	mes "sangat tidak asing";
	mes "salah satunya terlihat";
	mes "di danau. warna birunya";
	mes "sangat cerah pada";
	mes "alur permukaannya.^000000";
	next;
	switch( select( "sentuh relif", "lepas item dari lelif" ) )
	{
		case 1:
			mes "^3355FF*Cring Cring Cring*^000000";
			next;
			mes "^3355FFTiba2 munculah cahaya yang";
			mes "sangat menyilaukan mata";
			mes "Kamu mendengar suara gemuruh";
			mes "*grugug* *grugug* *grugug*";
			mes "dari dalam relif itu.^000000";
			specialeffect EF_WINDHIT;
			next;
			mes "*Ggghhhhhzzzz!*";
			mes "*BAM!*";
			next;
			mes "^3355FFCahaya yang bersinar";
			mes "relif tersebut membesar";
			mes "kamu merasakan cahaya tersebut";
			mes "menyilaukan, tanah pijakan ";
			mes "perlahan lahan mulai tenggelam...^000000";
			next;
			specialeffect EF_BOWLINGBASH;
			specialeffect2 EF_PORTAL;
			close2;
			warp "hu_fild05",169,305;
			end;

		case 2:
			mes "^3355FF*Ppppsssh!*";
			mes "Kamu sengaja";
			mes "memecahkan item tersebut.^000000";
			specialeffect EF_WINDHIT;
			next;
			mes "^3355FF*Gggggggghhhhhhhhzzzzzzjjjjhh!*^000000";
			next;
			mes "^3355FFCahaya yang berasal dari";
			mes "relif sangat menyilaukan";
			mes "dan relif tersebut mulai bergetar.";
			mes "membuat gempa bumi sehingga";
			mes "kamu sangat sulit untuk berdiri";
			mes "kamu mencoba bertahan, tetapi";
			mes "kamu berpindah ke tempat lain...^000000";
			next;
			specialeffect EF_BOWLINGBASH;
			specialeffect2 EF_PORTAL;
			close2;
			warp "hu_fild05",157,284;
			end;
	}
}

hu_fild05,196,210,1	script	AbyssWarp	45,2,2,{

OnInit:
	disablenpc "AbyssWarp";
	end;

OnTouch:
	warp "abyss_01",260,268;
	end;

OnWarp:
	initnpctimer;
	enablenpc "AbyssWarp";
	end;

OnTimer30000:
	stopnpctimer;
	disablenpc "AbyssWarp";
	end;
}
