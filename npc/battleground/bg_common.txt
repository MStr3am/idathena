//===== rAthena Script ======================================= 
// BattleGround System - Common NPCs
//===== By: ================================================== 
//= ????, L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.3
//===== Compatible With: ===================================== 
//= rAthena 1.0
//===== Description: ========================================= 
//= [AEGIS Conversion]
//= Battleground NPCs:
//= - Generals and Aides
//= - Battleground Warper
//= - Kafra and Repairman.
//= - GM Management NPC (disabled by default)
//= - Badge Exchanger (Tierra and Flavius)
//===== Additional Comments: ================================= 
//= 1.0 First Version.
//= 1.1 Updated several NPCs to Official.
//= 1.2 Updated Repairman NPC
//= 1.3 Optimized "Erundek" NPC. [Euphy] 
//============================================================
//===== Translated By: =======================================
//= iwangunner
//============================================================ 

// Generals
//============================================================
bat_room,161,158,3	script	Gen. Guillaume's Aide#01	419,{ end; }
bat_room,161,160,3	script	Gen. Guillaume's Aide#03	419,{ end; }

bat_room,160,141,3	script	Prince Croix	416,{
	cutin "bat_crua1",2;
	mes "[Prince Croix]";
	mes "Petualang yang bijaksana, kenapa tidak Anda meminjamkan kekuatan Anda untuk kemenangan?";
	next;
	switch(select("apa alasan ikut Battle?:kasi tau tentang General Guillaume")) {
	case 1:
		cutin "bat_crua2",2;
		mes "[Prince Croix]";
		mes "Maroll's raja yang besar, Marcel Marollo VII, sedang sakit akhir-akhir ini.";
		mes "Yang Mulia telah menyatakan bahwa ia akan meninggalkan masa depan Maroll kepada saya atau pangeran ketiga, General Guillaume.";
		next;
		mes "[Prince Croix]";
		mes "General Guillaume mungkin memiliki kelebihan dalam pertempuran ini karena ia adalah jenderal besar dari Maroll, tapi itu tidak otomatis dia akan menang.";
		mes "Saya ingin memenangkan pertempuran ini sehingga saya bisa membawa kemakmuran bagi rakyat Maroll. Mereka sudah cukup menderita dari perang...";
		next;
		switch(select("YYa, aku ikut dah.:akhiri pembicaraann")) {
		case 1:
			cutin "bat_crua1",2;
			mes "[Prince Croix]";
			mes "Terima kasih banyak. Saya merasa seperti saya bisa menang dengan bantuan petualang seperti Anda.";
			mes "Sekarang, silahkan pergi ke bawah dan bergabung rekan Anda dalam mengasah keterampilan mereka untuk melawan musuh!";
			break;
		case 2:
			mes "[Prince Croix]";
			mes "untuk Maroll!";
			break;
		}
		break;
	case 2:
		cutin "bat_crua2",2;
		mes "[Prince Croix]";
		mes "Pangeran ketiga Guillaume adalah jendral besar dari Maroll.";
		mes "Buang-buang waktu untuk menjelaskan kepada Anda betapa besar seorang pemimpin atau panglima perang dia, sejak dia memerintahkan kekuatan militer besar Maroll.";
		next;
		mes "[Prince Croix]";
		mes "Sayangnya, ada sesuatu yang ia dan para pengikutnya tidak menyadari:";
		mes "Apakah rakyat Maroll benar-benar ingin mereka untuk menghabiskan uang begitu banyak kekuatan militer?";
		mes "Kami telah cukup menderita akibat perang.";
		mes "Saya percaya senjata bukan cara terbaik untuk membawa kemakmuran bagi bangsa.";
		next;
		mes "[Prince Croix]";
		mes "Saya tidak ingin menumpahkan darah, tapi saya tidak punya pilihan selain berjuang untuk kemungkinan perdamaian dan demi umat-Ku.";
		next;
		switch(select("Yah gw gabung.:akhiri pembicaraan")) {
		case 1:
			cutin "bat_crua1",2;
			mes "[Prince Croix]";
			mes "Terima kasih banyak. Saya merasa seperti saya bisa menang dengan bantuan petualang seperti Anda.";
			mes "Sekarang, silahkan pergi ke bawah dan bergabung rekan Anda dalam mengasah keterampilan mereka untuk melawan musuh!";
			break;
		case 2:
			mes "[Prince Croix]";
			mes "untuk Maroll!";
			break;
		}
		break;
	}
	close2;
	cutin "bat_crua1",255;
	cutin "bat_crua2",255;
	end;
}

bat_room,161,140,3	script	Prince Croix's Aide#01	415,{ end; }
bat_room,161,142,3	script	Prince Croix's Aide#02	415,{ end; }

bat_room,160,159,3	script	General Guillaume	420,{
	cutin "bat_kiyom2",2;
	mes "[General Guillaume]";
	mes "darah panas petualang, kita perlu kemampuan Anda untuk memenangkan pertempuran ini.";
	next;
	switch(select("apa alasan nya mustiBattle?:kasi tau tentang pangeran Croix")) {
	case 1:
		cutin "bat_kiyom1",2;
		mes "[General Guillaume]";
		mes "raja kita yang besar, Marcel Marollo VII, sangat sakit akhir-akhir ini.";
		mes "yang mulia mendeklarasikan dia memilih antara aku atau Prince Croix sebagai raja berikutnya antara 9 putranya.";
		next;
		mes "[General Guillaume]";
		mes "Dua raja tidak dapat berbagi sebuah bangsa! Hanya satu kemenangan dari pertempuran Mulia ditunjuk akan bertahta.";
		next;
		mes "[General Guillaume]";
		mes "Hal ini, bagaimanapun, bukan hanya pertarungan antara kami. Pertempuran ini akan menentukan masa depan negara ini.";
		mes "Saya berjanji pada kehormatan saya untuk membuktikan bahwa aku orang yang dapat melindungi Maroll ini dari ancaman luar.";
		next;
		switch(select("Ya saya ikut.:End Conversation")) {
		case 1:
			cutin "bat_kiyom2",2;
			mes "[General Guillaume]";
			mes "Selamat Datang di tentara saya, kawan.";
			mes "Mata Anda memberitahu saya bahwa Anda adalah seorang prajurit yang bisa saya percaya.";
			next;
			mes "[General Guillaume]";
			mes "Sekarang, pergi ke atas dan berlaku untuk pertempuran dengan rekan-rekan Anda.";
			mes "Saya yakin mereka akan menyambut Anda dengan sepenuh hati!";
			break;
		case 2:
			mes "[General Guillaume]";
			mes "Saya akan menjadi orang yang akan menangkap bendera!";
			break;
		}
		break;
	case 2:
		cutin "bat_kiyom1",2;
		mes "[General Guillaume]";
		mes "The 5th Prince Croix saat ini dia sebagai Perdana Menteri Maroll.";
		mes "Dia pikir semua masalah nasional suatu bangsa dapat dibahas dan ditentukan di atas meja,";
		mes "dan percaya pada ko-eksistensi damai dengan negara lain.";
		next;
		mes "[General Guillaume]";
		mes "Dia terlalu bodoh untuk mengakui bahwa apa yang disebut perdamaian dibangun di atas hidup orang banyak yang dikorbankan dalam perang sementara warga biasa dan kelas atas bisa hidup, tidak menyadari kengerian yang memungkinkan mereka untuk hidup seperti itu.";
		next;
		mes "[General Guillaume]";
		mes "Dia terlalu naif untuk memahami realitas....";
		mes "Aku tidak bisa meninggalkan Maroll untuk seseorang seperti dia yang tinggal dalam mimpi!";
		next;
		mes "[General Guillaume]";
		mes "Keyakinan realistis Nya akan tenggelam dalam kemiskinan negara ini dan membuat orang lemah. Jika ia menjadi raja, Maroll tidak akan pernah beristirahat dari serangan gencar dari negara lain.";
		mes "Saya ingin mengajarkan kepadanya apa yang membuat negara kecil ini begitu kuat dan sejahtera. Ini kekuatan militer!";
		next;
		switch(select("Ya saya ikut jadi tentara!:End Conversation")) {
		case 1:
			cutin "bat_kiyom2",2;
			mes "[General Guillaume]";
			mes "Selamat Datang di tentara saya, kawan.";
			mes "Mata Anda memberitahu saya bahwa Anda adalah seorang prajurit yang bisa saya percaya.";
			next;
			mes "[General Guillaume]";
			mes "sekarang , pergi ke atas dan apply pertarungan untuk bergabung dengan kawan-kawan mu .";
			mes "Saya yakin mereka akan menyambut Anda dengan sepenuh hati!";
			break;
		case 2:
			mes "[General Guillaume]";
			mes "Saya akan menjadi orang yang akan menangkap bendera!";
			break;
		}
		break;
	}
	close2;
	cutin "bat_kiyom1",255;
	cutin "bat_kiyom2",255;
	end;
}

// Flags
//============================================================
//bat_room,140,160,3	script	Guillaume Base#roomflag1	973,{ end; }
//bat_room,167,160,3	script	Guillaume Base#roomflag2	973,{ end; }
//bat_room,140,139,3	script	Croix Base#roomflag1	974,{ end; }
//bat_room,167,139,3	script	Croix Base#roomflag2	974,{ end; }

// BattleGround Warper
//============================================================
bat_room,148,150,5	script	Teleporter#Battlefield	124,{
	mes "[Teleporter]";
	mes "Do you wish to leave the battlefield? Use my services to return to town.";
	next;
	switch(select("Leave:Don't Leave")) {
	case 1:
		mes "[Teleporter]";
		switch(bat_return) {
		default:
		case 1:
			setarray .@mapname$[0],"Prontera.","prontera";
			setarray .@xy[0],116,72;
			break;
		case 2:
			setarray .@mapname$[0],"Morroc","moc_ruins";
			setarray .@xy[0],152,48;
			break;
		case 3:
			setarray .@mapname$[0],"Al De Baran.","aldebaran";
			setarray .@xy[0],168,112;
			break;
		case 4:
			setarray .@mapname$[0],"Geffen.","geffen";
			setarray .@xy[0],120,39;
			break;
		case 5:
			setarray .@mapname$[0],"Payon.","payon";
			setarray .@xy[0],161,58;
			break;
		case 6:
			setarray .@mapname$[0],"Lighthalzen.","lighthalzen";
			setarray .@xy[0],159,93;
			break;
		case 7:
			setarray .@mapname$[0],"Rachel.","rachel";
			setarray .@xy[0],115,124;
			break;
		}
		mes "You will be sent back to "+.@mapname$[0]+".";
		close2;
		warp .@mapname$[1],.@xy[0],.@xy[1];
		break;
	case 2:
		mes "[Teleporter]";
		mes "saya akan disini kapanpun kamu butuh jasa saya.";
		close;
	}
	end;
}

-	script	Maroll Battle Recruiter::BatRecruit	728,{
	mes "[Maroll Battle Recruiter]";
	mes "Hari yang bagus , petualang.";
	mes "Saya seorang ksatria dari negeri yang jauh disebut Kerajaan Maroll.";
	next;
	mes "[Maroll Battle Recruiter]";
	mes "Dua pangeran kerajaan sekarang berjuang untuk tahta Maroll, dan membutuhkan tentara yang berpengalaman seperti Anda.";
	mes "Bagaimana Anda ingin meminjamkan kekuatan Anda ke salah satu pangeran di Kerajaan Maroll?";
	next;
	switch(select("Join:Don't Join")) {
	case 1:
		mes "[Maroll Battle Recruiter]";
		mes "Semoga dewa perang memberkati Anda.";
		close2;
		getmapxy(.@mapname$,.@x,.@y,1);
		if (.@mapname$ == "prontera")
			set bat_return,1;
		else if (.@mapname$ == "moc_ruins")
			set bat_return,2;
		else if (.@mapname$ == "aldebaran")
			set bat_return,3;
		else if (.@mapname$ == "geffen")
			set bat_return,4;
		else if (.@mapname$ == "payon")
			set bat_return,5;
		else if (.@mapname$ == "lighthalzen")
			set bat_return,6;
		else if (.@mapname$ == "rachel")
			set bat_return,7;
		else
			set bat_return,1;
		warp "bat_room",154,150;
		break;
	case 2:
		mes "[Maroll Battle Recruiter]";
		mes "Aku akan selalu ditempatkan di sini untuk lebih banyak tentara. Jangan ragu untuk datang kembali setiap kali Anda tertarik.";
		close;
	}
	end;
}

prontera,123,83,3	duplicate(BatRecruit)	Maroll Battle Recruiter::BatRecruit1	728
moc_ruins,75,162,3	duplicate(BatRecruit)	Maroll Battle Recruiter::BatRecruit2	728
aldebaran,146,109,3	duplicate(BatRecruit)	Maroll Battle Recruiter::BatRecruit3	728
geffen,109,66,3	duplicate(BatRecruit)	Maroll Battle Recruiter::BatRecruit4	728
payon,189,105,3	duplicate(BatRecruit)	Maroll Battle Recruiter::BatRecruit5	728
lighthalzen,153,86,5	duplicate(BatRecruit)	Maroll Battle Recruiter::BatRecruit6	728
rachel,149,138,3	duplicate(BatRecruit)	Maroll Battle Recruiter::BatRecruit7	728

// Additional warps
// Empty
bat_room,57,81,0	warp	bat1	1,1,bat_room,154,149
bat_room,57,90,0	warp	bat2	1,1,bat_room,154,149
// Empty
bat_room,85,81,0	warp	bat5	1,1,bat_room,154,149
bat_room,85,90,0	warp	bat6	1,1,bat_room,154,149
// Free BG
bat_room,113,81,0	warp	bat9	1,1,bat_room,154,149
bat_room,113,90,0	warp	bat10	1,1,bat_room,154,149
// Free BG
bat_room,141,81,0	warp	bat13	1,1,bat_room,154,149
bat_room,141,90,0	warp	bat14	1,1,bat_room,154,149
// Free BG
bat_room,169,81,0	warp	bat17	1,1,bat_room,154,149
bat_room,169,90,0	warp	bat18	1,1,bat_room,154,149
// Free BG
bat_room,197,81,0	warp	bat21	1,1,bat_room,154,149
bat_room,197,90,0	warp	bat22	1,1,bat_room,154,149
// Free BG
bat_room,225,81,0	warp	bat25	1,1,bat_room,154,149
bat_room,225,90,0	warp	bat26	1,1,bat_room,154,149
// Empty
bat_room,253,81,0	warp	bat29	1,1,bat_room,154,149
bat_room,253,90,0	warp	bat30	1,1,bat_room,154,149
// Empty
bat_room,253,220,0	warp	bat31	1,1,bat_room,154,149
bat_room,253,211,0	warp	bat32	1,1,bat_room,154,149

// Kafra
//============================================================
bat_room,148,147,4	script	Kafra Staff::kaf_bat	861,{
	cutin "kafra_09",2;
	callfunc "F_Kafra",0,2,1,150,0;
}

// Repairman
//============================================================
bat_room,138,144,4	script	Repairman#bg	86,{
	callfunc "repairmain","Repairman";
	end;
}

/*
// GM Management NPC
//============================================================
bat_room,1,151,3	script	Switch#batgnd	81,{
	input .@input,0,2000;
	if (.@input == 0) {
		mes "perintah dibatalkan.";
		close;
	}
	else if (.@input == 1854) {
		mes "mungkin aku bisa menolong mu?";
		next;
		switch(select("Close Battlefield:Open Battlefield:Reset a01:Reset b01:Reset a02:Reset b02")) {
		case 1:
			disablenpc "Tierra Gorge Officer#01a";
			disablenpc "Tierra Gorge Officer#02a";
			disablenpc "Tierra Gorge Officer#01b";
			disablenpc "Tierra Gorge Officer#02b";
			disablenpc "Flavius Officer#01a";
			disablenpc "Flavius Officer#01b";
			disablenpc "Flavius Officer#02a";
			disablenpc "Flavius Officer#02b";
			break;
		case 2:
			enablenpc "Tierra Gorge Officer#01a";
			enablenpc "Tierra Gorge Officer#02a";
			enablenpc "Tierra Gorge Officer#01b";
			enablenpc "Tierra Gorge Officer#02b";
			enablenpc "Flavius Officer#01a";
			enablenpc "Flavius Officer#01b";
			enablenpc "Flavius Officer#02a";
			enablenpc "Flavius Officer#02b";
			break;
		case 3:
			donpcevent "start#bat_a01::OnEnable";
			break;
		case 4:
			donpcevent "start#bat_b01::OnEnable";
			break;
		case 5:
			donpcevent "start#bat_a02::OnEnable";
			break;
		case 6:
			donpcevent "start#bat_b02::OnEnable";
			break;
		}
		mes "Complete";
		close;
	}
}
*/

// Badges Exchange
//============================================================
bat_room,160,150,3	script	Erundek	109,{
	if (checkweight(1201,1) == 0) {
		mes "- Tunggu sebentare !! -";
		mes "- sepertinya anda membawa-";
		mes "- terlalu banyak items di kantong anda. -";
		mes "- tolong ulangi lagi -";
		mes "- setelah beberapa barang anda tanggalkan. -";
		close;
	}
	mes "[Erundek]";
	mes "Apakah kamu punya battlefield badges?";
	mes "Saya bisa menukarkan Bravery Badges dan Valor Badges untuk sebuah items hadiah.";
	next;
	switch(select("penukaran Badges:cek Catalog")) {
	case 1:
		mes "[Erundek]";
		mes "Jenis item yang ingin kamu bertukar?";
		mes "Untuk memeriksa informasi lebih lanjut tentang item hadiah, silakan gunakan  ^3131FFCatalog^000000.";
		next;
		switch(select("Weapon:Armor:Accessory:Consumable")) {
		case 1:
			mes "[Erundek]";
			mes "Kamu memilih ^3131FFWeapon^000000.";
			mes "Senjata-senjata berikut tersedia untuk pertukaran dengan lencana medan.";
			mes "Harap dicatat bahwa item untuk ^3131FFBravery Badges are indicated as (BB)^000000, and ^3131FFValor Badges as (VB)^000000.";
			next;
			switch(select("Dagger/OneSword/TwoSword/TwoSpear:Staff/Mace/TwoAxe/Shuriken:Bow/Katar/Music/Whip:Book/Knuckle:Revolver/Rifle/Gatling/Shotgun/Launcher")) {
			case 1:
					mes "[Erundek]";
					mes "Item berikut ini tersedia di kategori ^3131FFDagger, One-Handed Sword, Two-Handed Sword, and Two-Handed Spear^000000.";
					next;
					setarray .@Weapons[0],13036,7828,13037,7829,13411,7828,13410,7829,1183,7828,1184,7829,1425,7828,1482,7829;
					break;
			case 2:
					mes "[Erundek]"; 
					mes "Item berikut ini tersedia di kategori ^3131FFStaff / Mace / Two-Handed Axe / Huuma Shuriken^000000.";
					next;
					setarray .@Weapons[0],1632,7828,1633,7829,1634,7828,1635,7829,1543,7828,1542,7829,1380,7828,1379,7829,13305,7828,13306,7829; 
					break;
			case 3: 
					mes "[Erundek]"; 
					mes "Item berikut ini tersedia di kategori^3131FFBow / Katar / Musical Instrument / Whip^000000.";
					next;
					setarray .@Weapons[0],1739,7828,1738,7829,1279,7828,1280,7829,1924,7828,1923,7829,1978,7828,1977,7829; 
					break;
			case 4: 
					mes "[Erundek]"; 
					mes "Item berikut ini tersedia di kategori ^3131FFBook / Knuckle^000000.";
					next;
					setarray .@Weapons[0],1574,7828,1575,7829,1824,7828,1823,7829; 
					break;
			case 5: 
					mes "[Erundek]"; 
					mes "Item berikut ini tersedia di kategori ^3131FFRevolver / Rifle / Gatling Gun / Shotgun / Grenade Launcher^000000.";
					next;
					setarray .@Weapons[0],13108,7828,13171,7829,13172,7828,13173,7829,13174,7829; 
					break;
			}
			set .@menu$,""; 
			for(set .@i,0; .@i<getarraysize(.@Weapons); set .@i,.@i+2) 
					set .@menu$, .@menu$+getitemname(.@Weapons[.@i])+((.@Weapons[.@i+1]==7828)?"(BB)":"(VB)")+":"; 
			set .@i, (select(.@menu$)-1)*2; 
			set .@type$, ((.@Weapons[.@i+1]==7828)?"(BB)":"(VB)"); 
			mes "[Erundek]"; 
			mes "Kamu memilih ^3131FF"+getitemname(.@Weapons[.@i])+.@type$+"^000000."; 
			mes "Kamu bisa memiliki item ini dengan menukarkan ^FF0000100 "+getitemname(.@Weapons[.@i+1])+"^000000."; 
			mes "Maukah kamu menukarkannya?"; 
			next;
			switch(select("Jangan tukar:Tukar")) { 
			case 1: 
				break;
			case 2: 
				mes "[Erundek]"; 
				mes "Maukah kamu menukarkan ^FF0000100 "+getitemname(.@Weapons[.@i+1])+"^000000 untuk menerima sebuah ^3131FF"+getitemname(.@Weapons[.@i])+.@type$+"^000000?";
				next; 
				mes "[Erundek]"; 
				mes "Ingat, Hadiah Battleground adalah ^FF0000Character Bound^000000. Apakah kamu yakin menginginkan item ini?"; 
				next; 
				switch(select("Ya:Tidak")) { 
				case 1: 
					mes "[Erundek]"; 
					if (countitem(.@Weapons[.@i+1]) >= 100) { 
						mes "Terima kasih sudah menukarkannya."; 
						delitem .@Weapons[.@i+1],100; 
						getitem .@Weapons[.@i],1; 
					} 
					else mes "Maafkan saya, tapi kamu tidak punya cukup lencana untuk ditukarkan."; 
					close;
				case 2:
					break;
				}
				break;
			}
			mes "[Erundek]";
			mes "Kamu butuh waktu untuk memeriksa barangnya?";
			close;
		case 2:
			mes "[Erundek]";
			mes "Kamu memilih ^3131FFArmor^000000.";
			mes "Armor berikut tersedia untuk pertukaran dengan battlefield badges.";
			next;
			switch(select("Garments / Shoes:Armor")) {
			case 1:
				setarray .@Items[0],2538,50,2539,50,2540,50,2435,50,2436,50,2437,50;
				break;
			case 2:
				setarray .@Items[0],2376,80,2377,80,2378,80,2379,80,2380,80,2381,80,2382,80;
				break;
			}
			break;
		case 3:
			mes "[Erundek]";
			mes "Kamu memilih ^3131FFAccessory^000000.";
			mes "Kamu bisa menukarkan Medal of Honors dengan your Badges sesuai dengan job classes, as follows:";
			next;
			setarray .@Items[0],2733,500,2720,500,2721,500,2722,500,2723,500,2724,500,2725,500;
			set .@menu1$,"Gunslinger:Swordman/Taekwon Master:Thief:Acolyte:Magician:Archer:Merchant";
			break;
		case 4:
			mes "[Erundek]";
			mes "kamu memilih ^3131FFConsumable^000000.";
			mes "tem habis berikut tersedia untuk pertukaran dengan battlefield badges:";
			next;
			setarray .@Items[0],12269,10,12270,10,12271,5,12272,10,12273,10;
			break;
		}
		break;
	case 2:
		mes "[Erundek]";
		mes "Kami memiliki banyak item, jadi silakan melihat dan  membeli.";
		close2;
		ReadBook 11010,1;
		end;
	}
	set .@menu$,"";
	if (.@menu1$ != "") set .@menu$, .@menu1$;
	else for(set .@i,0; .@i<getarraysize(.@Items); set .@i,.@i+2)
		set .@menu$, .@menu$+getitemname(.@Items[.@i])+":";
	set .@i, (select(.@menu$)-1)*2;
	mes "[Erundek]";
	mes "Kamu memilih ^3131FF"+getitemname(.@Items[.@i])+"^000000.";
	switch(.@Items[.@i]) {
		case 2720: mes "Barang ini hanya untuk kelas Swordman and Taekwon Master."; break;
		case 2721: mes "Barang ini hanya untuk kelas Thief Class."; break;
		case 2722: mes "Barang ini hanya untuk kelas Acolyte Class."; break;
		case 2723: mes "Barang ini hanya untuk kelas Magician Class."; break;
		case 2724: mes "Barang ini hanya untuk kelas Archer Class."; break;
		case 2725: mes "Barang ini hanya untuk kelas Merchant Class."; break;
		case 2733: mes "Barang ini hanya untuk kelas Gunslinger."; break;
		default: break;
	}
	mes "Kamu bisa menukar item ini dengan ^FF0000"+.@Items[.@i+1]+" "+getitemname(7828)+" or "+.@Items[.@i+1]+" "+getitemname(7829)+"^000000.";
	mes "Apakah kamu akan bertukar?";
	next;
	switch(select("Jangan ditukar:Tukarkan")) {
	case 1:
		mes "[Erundek]";
		mes "Apakah Anda perlu lebih banyak waktu untuk memeriksa item?";
		break;
	case 2:
		mes "[Erundek]";
		mes "Apa Badge yang ingin kamu tukarkan?";
		mes "Kamu butuh ^3131FF"+.@Items[.@i+1]+" Badges^000000 untuk bertukar.";
		next;
		if (.@item[0] < 12269 || .@item[0] > 12273 ) {
			mes "[Erundek]";
			mes "Ingat, Battleground Reward Items are ^FF0000Character Bound^000000. apa kamu yakin ingin item ini?";
			next;
		}
		set .@j, select("Bravery Badge:Valor Badge:Batal");
		mes "[Erundek]";
		if (.@j == 3) {
			mes "Kamu membatalkan pertukaran.";
			break;
		}
		set .@cost, ((.@j==1)?7828:7829);
		if (countitem(.@cost) >= .@Items[.@i+1]) {
			mes "Terima kasih telah menukarkannya.";
			delitem .@cost, .@Items[.@i+1];
			getitem .@Items[.@i],1;
		}
		else mes "Kamu tidak punya cukup "+getitemname(.@cost)+"s.";
		break;
	}
	close;
}
